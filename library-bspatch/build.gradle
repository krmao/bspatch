buildscript {
    ext {
        buildToolsVersion = "31.0.0"
        minSdkVersion = 21
        compileSdkVersion = 31
        targetSdkVersion = 31
        kotlinVersion = '1.7.0'
        dokkaVersion = '1.7.10'

        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }

        name = "library-bspatch"
        versionCode = 1
        versionName = "0.0.3"

        bintrayRepo = "maven"
        bintrayName = "com.smart.library.util.bspatch"
        libraryName = "bspatch"

        publishedGroupId = "com.smart.library.util"
        artifact = "bspatch"
        libraryVersion = rootProject.ext.versionName

        libraryDescription = "bspatch utils for android"
        siteUrl = "https://github.com/krmao/bspatch"
        gitUrl = "https://github.com/krmao/bspatch.git"
        developerId = "krmao"
        developerName = "michael mao"
        developerEmail = "mkrcpp@qq.com"
        licenseName = "The Apache Software License, Version 2.0"
        licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        allLicenses = ["Apache-2.0"]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath("com.android.tools.build:gradle:7.0.4")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

android {
    ndkVersion rootProject.ext.ndkVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        archivesBaseName = "${rootProject.ext.name}-${rootProject.ext.versionName}"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-${rootProject.ext.minSdkVersion}"
                targets 'bspatch' // 不填就是全部编译
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "x86", "x86_64", "arm64-v8a"
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version '3.6.0-rc2'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

apply from: "./upload_to_maven_central.gradle"